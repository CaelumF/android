import com.toggl.buildsrc.Libs
import com.toggl.buildsrc.Versions

buildscript {
    ext.buildConfig = [
            'compileSdk': 29,
            'minSdk'    : 21,
            'targetSdk' : 29,
            'versionName' : "0.1",
            'versionCode' : 600000,
    ]
    repositories {
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap/"
        }
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Test.junit5Plugin
        classpath Libs.gradleVersionsPlugin
        classpath Libs.Google.googleServicesPluginClassPath
        classpath Libs.Google.firebaseCrashlyticsPluginClassPath
        classpath Libs.Google.firebasePerformancePluginClassPath
        classpath Libs.Hilt.gradlePlugin
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.28.0'
}

apply plugin: 'com.github.ben-manes.versions'
defaultTasks 'dependencyUpdates'

allprojects {
    repositories {
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap/"
        }
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(Versions.ktlint).userData(["disabled_rules": "final-newline,import-ordering"])
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true
            // Set JVM target to 1.8
            jvmTarget = "1.8"
            apiVersion = "1.4"
            languageVersion = "1.4"
            freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check"]
        }
    }
}